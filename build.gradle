import org.gradle.internal.os.OperatingSystem

plugins {
    id 'java'
    id 'application'
}

group 'br.com.camila'
version '0.0.1'

// The game will support only Windows x64 and MacOS
def lwjglNatives = OperatingSystem.current().isMacOsX()
        ? "natives-macos"
        : "natives-windows"

repositories {
    mavenCentral()
}

dependencies {
    implementation platform("org.lwjgl:lwjgl-bom:3.2.3")

    // LightWeight Java Game Library
    implementation "org.lwjgl:lwjgl"
    runtimeOnly "org.lwjgl:lwjgl::$lwjglNatives"

    // OpenGL native bind for Java
    implementation "org.lwjgl:lwjgl-opengl"
    runtimeOnly "org.lwjgl:lwjgl-opengl::$lwjglNatives"

    // Manage window and inputs
    implementation "org.lwjgl:lwjgl-glfw"
    runtimeOnly "org.lwjgl:lwjgl-glfw::$lwjglNatives"

    // Java OpenGL Math Library
    implementation 'org.joml:joml:1.10.2'

    // JSON
    implementation 'com.google.code.gson:gson:2.8.8'
}

mainClassName = 'br.com.camila.Application'